plugins {
    id "java"
    id("org.panteleyev.jpackageplugin") version "1.5.2"
}
group 'me.cortex'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.ow2.asm:asm:9.5'
    implementation 'org.ow2.asm:asm-tree:9.5'
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.register("copyDependencies", Copy.class) {
    from(configurations.runtimeClasspath).into("$buildDir/jars")
}

tasks.register("copyJar", Copy.class) {
    from(tasks.jar).into("$buildDir/jars")
}

tasks.jpackage {
    dependsOn("build", "copyDependencies", "copyJar")

    input = "$buildDir/jars"
    destination = "$buildDir/dist"

    appName = "Neko Detector"
    aboutUrl = "https://github.com/MCRcortex/nekodetector"
    appDescription = "A tool to help detect if you are infected by the fractureiser malware."

    licenseFile = "LICENSE"

    vendor = "me.cortex"

    mainJar = tasks.jar.archiveFileName.get()
    mainClass = "me.cortex.jarscanner.Main"

    javaOptions = ["-Dfile.encoding=UTF-8"]

    addModules = ["java.base", "java.desktop"]

    JLinkOptions = [
            "--strip-native-commands",
            "--strip-debug",
            "--no-man-pages",
            "--no-header-files",
            "--compress=2",
    ]


    windows {
        winConsole = true
        winPerUserInstall = true
        winHelpUrl = "https://github.com/MCRcortex/nekodetector"
        winShortcut = true
        winShortcutPrompt = true
        winMenu = true

        winUpdateUrl = "https://github.com/MCRcortex/nekodetector/releases"
    }

    mac {
        macAppCategory = "public.app-category.utilities"
        macPackageIdentifier = "me.cortex.nekodetector"
    }

    linux {
        linuxAppCategory = "Utility"
        linuxMenuGroup = "Utility;Anti-Malware;Security;"
        linuxShortcut = true
    }
}